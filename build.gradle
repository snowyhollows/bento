group = 'net.snowyhollows.bento'
version = '1.5-SNAPSHOT'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    repositories {
        mavenCentral()
    }

    jar {
        from sourceSets.main.allJava
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier 'javadoc'
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://maven.ceon.pl/artifactory/em-releases"
                def snapshotsRepoUrl = "https://maven.ceon.pl/artifactory/em-snapshot"
                url = rootProject.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username System.getenv("ARTIFACTORY_USR")
                    password System.getenv("ARTIFACTORY_PSW")
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testAnnotationProcessor project(':bento-generator')

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.mockito:mockito-core:3.11.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:3.11.2'
        testImplementation 'org.assertj:assertj-core:3.11.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }
}
